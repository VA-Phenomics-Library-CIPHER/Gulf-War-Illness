data GWI_PartA;
	set new;
	/* Step A1.  Symptoms from the Kansas and CDC Measures*/
	%Array (symptoms, values = &allSymptomsList);
	array symptom (&symptomsn) %DO_OVER(symptoms, phrase=?);		  		*build array of symptoms;
	array symptomRate(&symptomsn) %Do_OVER(symptoms, phrase=?rate);	  		*build array of symptomRates;
	array symptom_Rev(&symptomsn) %DO_OVER(symptoms, phrase=?_rev);	  		*build array of revised symptoms;
	array symptomRate_Rev(&symptomsn) %DO_OVER(symptoms, phrase=?rate_rev);	*build array of revised symptomRates;
	*Step A2.  Infer a symptom is present if responded rated the severity as mild, moderate or severe;
	do i=1 to &symptomsn;
		if symptom[i] in (&no_var) then symptom_REV[i]=0;  *revised symptom is the no if original symptom is no;
		if symptom[i] in (&yes_var) then symptom_REV[i]=1;	 *revised symptom is yes if original symptom is yes;
		if symptom[i] in (&missing_values) & symptomRate[i] in (&mild_var &moderate_var &severe_var) then symptom_Rev[i]=1; *if symptom rating is provided, that overrules the missing symptom value;
		if symptom[i] in (&no_var) & symptomRate[i] in (&mild_var &moderate_var &severe_var) then symptom_Rev[i]=1;  *if symptom rating is provided, that overrules the "no" symptom value;
		end;
	*Step A3.Create new variables that revise the severity score from 0 “No” to 3 “Severe”;
	do i=1 to &symptomsn;
		symptomRate_Rev[i]= . ;
		if symptomRate[i] in (&mild_var) then symptomRate_Rev[i] = 1	 ; *symptom rating = "yes, mild";
		if symptomRate[i] in (&moderate_var) then symptomRate_Rev[i] = 2	 ; *symptom rating = "yes, moderate";
		if symptomRate[i] in (&severe_var) then symptomRate_Rev[i] = 3	 ;  *symptom rating = "yes, severe";
		if symptomRate[i] in (&multiple_answers) then symptomRate_Rev[i] = .b	 ;  *symptom rating = "yes, multiple severities";
		if symptomRate[i] in (&missing_values) & symptom_Rev[i]= 0 then symptomRate_Rev[i]=0; *symptom rating = "no, none";
		end;
	*Format each type of variable by array;
	format %DO_OVER(symptoms, phrase=?) l_yesno.;
	format %Do_OVER(symptoms, phrase=?rate) l_Rate.;
	format %DO_OVER(symptoms, phrase=?_rev) l_yesno.;
	format %DO_OVER(symptoms, phrase=?rate_rev) l_RevisedRate.;
run;
